TRACE ***************************************************************** 
       Дебиторка 
      *****************************************************************;

LET vLoad = Time(Now(1));

[Связи]:
LOAD 
 	%Дата as %Дата66
    ,%Дата as сДата
 RESIDENT Календарь;

[ВремВЗ_2]:
LEFT KEEP (ДоговораОтбор)
LOAD
	_Fld4788RRef as %ДоговорОтбор
    ,AutoNumber(_Fld4789_RRRef, 'Регистратор') as %Регистратор66 // Сделка
    ,AutoNumber(_Fld4790RRef, 'Контрагент') as %Контрагент66
    ,AutoNumber(_Fld4788RRef, 'ДоговорКонтрагента') as %Договор66
    ,Floor(_Period - $(vDateOffset)) as ДатаВзаиморасчетов //Floor(_Period - $(vDateOffset))
    ,RowNo() as НомерСтрокиДокумента
    ,Num(If(Not _RecordKind, _Fld4791, - _Fld4791)) as СуммаВзаиморасчетов
    ,Num(If(Not _RecordKind, _Fld4792, - _Fld4792)) as СуммаУпр
;SQL SELECT
    _Period // as Период
    ,_RecordKind // as ВидДвижения
    ,_Fld4790RRef // as Контрагент
    ,_Fld4788RRef // as Договор
    ,_Fld4789_RRRef // as Сделка
    ,_Fld4791 // as СуммаВзаиморасчетов
    ,_Fld4792 // as СуммаУпр
//    ,_RecorderTRef //_Fld8758_RTRef  //_Fld8758_TYPE
    ,_Active
FROM $(vBaseName).dbo._AccumRg4787 WHERE _Active = 1;

DROP Field %ДоговорОтбор from ВремВЗ_2;

//CALL SaveTxt('ВремВЗ_2', 'ВремВЗ_2.csv');

[СрокиОплаты]:
LOAD Distinct
    %Регистратор
    ,СрокОплаты
RESIDENT Реализации;

CONCATENATE (СрокиОплаты)
LOAD
    AutoNumber(_IDRRef, 'Регистратор') as %Регистратор
    ,Floor(RangeMax(_Fld2848, _Date_Time) - $(vDateOffset)) as СрокОплаты
;SQL SELECT
    _IDRRef
    ,_Date_Time
    ,_Fld2848
FROM $(vBaseName).dbo._Document171;

LEFT JOIN (ВремВЗ_2)
LOAD Distinct
    %Регистратор as %Регистратор66
    ,СрокОплаты
RESIDENT СрокиОплаты;

DROP FIELDS СрокОплаты, СуммаРеализацииУпр from Реализации;
DROP TABLE СрокиОплаты;

//**************************************************************************
// Для корректных рассчетов важна именно такая сортировка и группировка.
//**************************************************************************
[ВЗ]:
NOCONCATENATE LOAD 
	If(IsNum(ДатаВзаиморасчетов), ДатаВзаиморасчетов, $(#vBeginTime)) as НачальнаяДата
    ,%Регистратор66
    ,%Контрагент66
    ,%Договор66
    ,FirstValue(СрокОплаты) as СрокОплаты
    ,Num(Sum(СуммаВзаиморасчетов)) as СуммаВзаиморасчетов
    ,Num(Sum(СуммаУпр)) as СуммаУпр
    ,Num(Round(If([%Контрагент66] = Peek('%Контрагент66') and [%Договор66] = Peek('%Договор66') and [%Регистратор66] = Peek('%Регистратор66'), RangeSum(Sum(СуммаУпр), Peek('ДолгУпр')), Sum(СуммаУпр)), 0.01)) as ДолгУпр
    ,Num(Round(If([%Контрагент66] = Peek('%Контрагент66') and [%Договор66] = Peek('%Договор66') and [%Регистратор66] = Peek('%Регистратор66'), RangeSum(Sum(СуммаВзаиморасчетов), Peek('Долг')), Sum(СуммаВзаиморасчетов)), 0.01)) as Долг
RESIDENT ВремВЗ_2
GROUP BY %Контрагент66, %Договор66, %Регистратор66, ДатаВзаиморасчетов
ORDER BY %Контрагент66, %Договор66, %Регистратор66, ДатаВзаиморасчетов;

DROP TABLE ВремВЗ_2;

//CALL SaveTxt('ВЗ', 'ВЗ.csv');

[Взаиморасчеты]:
LOAD Distinct
    НачальнаяДата
    ,КонечнаяДата
    ,%Регистратор66
    ,%Контрагент66
    ,%Договор66
    ,СрокОплаты
    ,Долг
    ,ДолгУпр
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал;
LOAD 
	НачальнаяДата
    ,If(%Регистратор66 = Peek('%Регистратор66') and [%Контрагент66] = Peek('%Контрагент66') and [%Договор66] = Peek('%Договор66'), Peek('НачальнаяДата') - $(#vEpsilon), $(#vEndTime)) as КонечнаяДата
    ,%Регистратор66
    ,%Контрагент66
    ,%Договор66
    ,СрокОплаты
    ,Долг
    ,ДолгУпр
RESIDENT ВЗ
ORDER BY %Регистратор66, %Контрагент66, %Договор66, НачальнаяДата Desc;
DROP TABLE ВЗ;

//CALL SaveTxt('Взаиморасчеты', 'Взаиморасчеты1.csv');

[ВремСвязи]: 
IntervalMatch (сДата)
LOAD Distinct 
	НачальнаяДата
    ,КонечнаяДата
RESIDENT Взаиморасчеты;

JOIN (Связи)
LOAD  
	сДата
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал
RESIDENT ВремСвязи;
DROP TABLE ВремСвязи; 
DROP FIELD сДата;

LEFT JOIN (Взаиморасчеты)
LOAD
	%Интервал
    ,%Дата66
RESIDENT Связи;
DROP TABLE Связи;
DROP FIELDS НачальнаяДата, КонечнаяДата, %Интервал from Взаиморасчеты;

//CALL SaveTxt('Взаиморасчеты', 'Взаиморасчеты2.csv');

[Взаиморасчеты2]:
NOCONCATENATE LOAD
    *
	,AutoNumber(%Дата66 & '|' & %Регистратор66 & '|' & %Контрагент66 & '|' & %Договор66, 'ДатаРегистраторКонтрагентДоговор') as %ДатаРегистраторКонтрагентДоговор
    ,Floor(If(Долг > 0.01, 1, 0)) as ДЛГ
    ,Floor(If(Долг < 0, 1, 0)) as НДЛГ
    ,Floor(If(%Дата66 > СрокОплаты and not IsNull(СрокОплаты) and Долг > 0.01, 1, 0)) as ПДЛГ
    ,Floor(If(%Дата66 > СрокОплаты and not IsNull(СрокОплаты) and Долг > 0.01, %Дата66 - СрокОплаты, 0)) as Просрочка
RESIDENT Взаиморасчеты
WHERE Долг <> 0 
ORDER BY %Дата66 desc, %Контрагент66, %Договор66, %Регистратор66;

//CALL SaveTxt('Взаиморасчеты2', 'Взаиморасчеты4.csv');

DROP Table Взаиморасчеты;
RENAME Table Взаиморасчеты2 to Взаиморасчеты;

CONCATENATE (ТаблицаСвязей)
LOAD Distinct
    %ДатаРегистраторКонтрагентДоговор
    ,%Дата66 as %Дата
    ,%Регистратор66 as %Регистратор
    ,%Контрагент66 as %Контрагент
    ,%Договор66 as %Договор
    ,Null () as %Менеджер
    ,Null () as %МесяцГодМенеджер
    ,Null () as %ДатНомХарСклКонтр
    ,Null () as %ДатНомХарСклКонМенМгодРегистр
    ,Null () as %ДатНомХарСкл
    ,Null () as %ДатНомСкл
    ,Null () as %МесяцГод
    ,Null () as %ИдЦенаНаРеализации
RESIDENT Взаиморасчеты;

DROP Fields %Дата66, %Регистратор66, %Контрагент66, %Договор66 from Взаиморасчеты;

LET vLoad = Time(Time(Now(1)) - vLoad);
TRACE
Время загрузки: $(vLoad)
;
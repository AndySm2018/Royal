TRACE ***************************************************************** 
       Остатки и движения 
      *****************************************************************;

LET vLoad = Time(Now(1));

[Связи]:
LOAD 
 	%Дата
    ,%Дата as сДата
 Resident Календарь;

[ВремДвижения]:
LEFT KEEP (Cклады)
LOAD
   	Floor(_Period - $(vDateOffset)) as ДатаД
    ,RowNo() as НСтроки
    ,If(_RecordKind = 0, 'Приход', 'Расход') as ВидДвижения
    ,AutoNumber(_Fld5401RRef, 'Склад') as %Склад
    ,_Fld5401RRef as %СкладОтбор
    ,AutoNumber(_Fld5402RRef, 'Номенклатура') as %Номенклатура2
    ,AutoNumber(_Fld5403RRef, 'Характеристика') as %Характеристика2
    ,AutoNumber(_Fld5404RRef, 'Серия') as %Серия
    ,Floor(If(_RecordKind = 0, "_Fld5405", -"_Fld5405")) as Количество
    ,Num(If(_RecordKind = 0, _Fld5405, 0)) as Приход
    ,Num(If(_RecordKind <> 0, _Fld5405, 0)) as Расход
;SQL SELECT 
    _Period
    ,_RecordKind
    ,_Fld5401RRef
    ,_Fld5402RRef
    ,_Fld5403RRef
    ,_Fld5404RRef
    ,_Fld5405
    ,_Active
FROM $(vBaseName).dbo._AccumRg5400 WHERE _Active = 1;

DROP Field %СкладОтбор from ВремДвижения;

//CALL SaveTxt('ВремДвижения', 'ВремДвижения.csv')

//---------------------------------------------------------------------------------
// Для корректного получения остатков в разрезе номенклатура, дата, склад
// важно соблюдать такой принцип группировки и сортировки. Он меняется при расчете
// движений и остатков.
// При расчете остатка и начальной даты важно учитывать предыдущую номенклатуру
// и склад.
//---------------------------------------------------------------------------------

[Движения]:
NOCONCATENATE LOAD
	ДатаД
	,%Номенклатура2
    ,%Характеристика2
	,%Склад as %Склад2
    ,ВидДвижения
    ,Num(Sum(Приход)) as Приход
    ,Num(Sum(Расход)) as Расход
RESIDENT ВремДвижения
GROUP BY %Номенклатура2, %Характеристика2, ДатаД, %Склад, ВидДвижения
ORDER BY %Номенклатура2, ДатаД Asc, %Характеристика2, %Склад; 

[ВремОстатки]:
NOCONCATENATE LOAD 
	%Номенклатура2 as %Номенклатура
    ,%Характеристика2 as %Характеристика
    ,%Серия as %СерияОстатка
    ,If(IsNum(ДатаД), ДатаД, $(#vBeginTime)) as НачальнаяДата
	,%Склад
	,Num(Sum(Количество)) as Количество
    ,Num(If([%Номенклатура2] = Peek('%Номенклатура') and [%Характеристика2] = Peek('%Характеристика') and [%Серия] = Peek('%СерияОстатка') and [%Склад] = Peek('%Склад'), RangeSum(Sum(Количество), Peek('Остаток')), Sum(Количество))) as Остаток
RESIDENT ВремДвижения
GROUP BY %Номенклатура2, %Характеристика2, %Серия, %Склад, ДатаД
ORDER BY %Номенклатура2, %Характеристика2, %Серия, %Склад, ДатаД Asc;

DROP TABLE ВремДвижения;

//CALL SaveTxt('ВремОстатки', 'ВремОстатки.csv')

[Остатки]:
LOAD Distinct
    НачальнаяДата
    ,КонечнаяДата
	,%Номенклатура
    ,%Характеристика
	,%Склад
    ,%СерияОстатка
	,Num(Остаток) as Остаток
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал;
LOAD 
	НачальнаяДата
    ,If(%Номенклатура = Peek('%Номенклатура') and [%Характеристика] = Peek('%Характеристика') and [%СерияОстатка] = Peek('%СерияОстатка') and [%Склад] = Peek('%Склад'), Peek('НачальнаяДата') - $(#vEpsilon), $(#vEndTime)) as КонечнаяДата
	,%Номенклатура
    ,%Характеристика
	,%Склад
    ,%СерияОстатка
    ,Остаток
RESIDENT ВремОстатки
ORDER BY %Номенклатура, %Характеристика, %СерияОстатка, %Склад, НачальнаяДата Desc;
DROP TABLE ВремОстатки;

//CALL SaveTxt('Остатки', 'Остатки1.csv')

[ВремСвязи]: 
IntervalMatch (сДата)
LOAD Distinct 
	НачальнаяДата
    ,КонечнаяДата
RESIDENT Остатки;

JOIN (Связи)
LOAD  
	сДата
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал
RESIDENT ВремСвязи;
DROP TABLE ВремСвязи; 
DROP FIELD сДата;

LEFT JOIN (Остатки)
LOAD
	%Интервал
    ,%Дата
RESIDENT Связи;
DROP TABLE Связи;
DROP FIELDS НачальнаяДата, КонечнаяДата, %Интервал from Остатки;

//CALL SaveTxt('Остатки', 'Остатки2.csv')

[Остатки2]:
NOCONCATENATE LOAD
    *
RESIDENT Остатки
WHERE Остаток <> 0;

DROP Table Остатки;
RENAME Table Остатки2 to Остатки;

LET vLoad = Time(Time(Now(1)) - vLoad);
TRACE
Время загрузки: $(vLoad)
;
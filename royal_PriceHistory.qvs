TRACE ***************************************************************** 
       075_Цены партий
      *****************************************************************;

LET vLoad = Time(Now(1));

[Связи]:
LOAD DISTINCT
 	%Дата3
    ,%Дата3 as сДата
 RESIDENT РегистрПродажи;
 
/*TODO Добавляем серии в цены партий
Загружаем только цены с серией и добавляем ключ СерияСценой. И только та номенклатура. что есть
в продажах.
Загружаем только цены без серии. И только та номенклатура. что есть
в продажах.
Расчитываем все цены в евро
Загружаем серии из продаж, кроме СерияСЦеной
Добавляем к номенклатуре и характеристике серии
Объеденяем с первой таблицей
Дальше продалжаем алгоритм
*/

[ВремЦены]:
LEFT KEEP (РегистрПродажи)
LOAD
	_Period - $(vDateOffset) as ДатаЦ
	,AutoNumber(_Fld4732RRef, 'Серия') as %Серия
    ,AutoNumber(_Fld4733RRef, 'ТипЦен') as %ТипЦен
	,Autonumber(_Fld4734RRef, 'Валюта') as %Валюта
	,Num(_Fld4735) as ЦенаПартии
;SQL SELECT
	_Period
	,_Fld4732RRef
	,_Fld4733RRef
	,_Fld4734RRef
	,_Fld4735
    ,_Active
FROM $(vBaseName).dbo._InfoRg4729 WHERE _Active = 1;

[Цены]:
NOCONCATENATE
LOAD Distinct
	If(IsNum(ДатаЦ), Floor(ДатаЦ), $(#vBeginTime)) as НачальнаяДата
	,FirstValue(%Серия) as %СерияП
	,FirstValue(%ТипЦен) as %ТипЦенП
	,FirstValue(%Валюта) as %Валюта
	,FirstValue(ЦенаПартии) as ЦенаПартии
RESIDENT ВремЦены
GROUP BY %Серия, %ТипЦен, %Валюта, ДатаЦ
ORDER BY ДатаЦ Desc;
DROP TABLE ВремЦены; 

[ЦеныПартий]:
LOAD Distinct
    НачальнаяДата
    ,КонечнаяДата
	,%СерияП
    ,%ТипЦенП
	,%Валюта
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал
    ,ЦенаПартии
;
LOAD 
	НачальнаяДата
    ,If(%СерияП = Peek('%СерияП') and [%ТипЦенП] = Peek('%ТипЦенП') and [%Валюта] = Peek('%Валюта'), Peek('НачальнаяДата') - $(#vEpsilon), $(#vEndTime)) as КонечнаяДата
	,%СерияП
    ,%ТипЦенП
	,%Валюта
    ,ЦенаПартии
RESIDENT Цены
ORDER BY %СерияП, %ТипЦенП, %Валюта, НачальнаяДата Desc;
DROP TABLE Цены;

[ВремСвязи]: 
IntervalMatch (сДата)
LOAD Distinct 
	НачальнаяДата
    ,КонечнаяДата
RESIDENT ЦеныПартий;

JOIN (Связи)
LOAD
	сДата
    ,Autonumber(Num(НачальнаяДата) & '|' & Num(КонечнаяДата), 'Интервал') as %Интервал
RESIDENT ВремСвязи;
DROP TABLE ВремСвязи; 
DROP FIELD сДата;

LEFT JOIN (ЦеныПартий)
LOAD 
	%Интервал
    ,%Дата3 as %Дата
RESIDENT Связи;
DROP TABLE Связи;
DROP FIELDS НачальнаяДата, КонечнаяДата, %Интервал from ЦеныПартий;

Call bjoin('РегистрПродажи');
LEFT JOIN (РегистрПродажи)
LOAD 
	%Дата as [%Дата3]
	,[%СерияП] as [%Серия]
    ,[%ТипЦенП] as [%ТипЦен]
    ,ApplyMap('УпрКурс', (%Дата & '_' & ApplyMap('Валюта', %Валюта)), 1) as Курс
    ,If(%Дата > 42551 and [ЦенаПартии] > 100000, [ЦенаПартии] / 10000, [ЦенаПартии]) as [ЦенаПартии]
    ,%Валюта
RESIDENT ЦеныПартий;
DROP TABLE ЦеныПартий;
Call ajoin('РегистрПродажи', 'Добавляем ТипЦен, Валюта, ЦенаПартии, Курс');

Call bjoin('РегистрПродажи');
LEFT JOIN (РегистрПродажи)
LOAD
    %ДатаКурса as %Дата3
    ,КурсUSD2 as КурсUSDпр
RESIDENT КурсUSD;
Call ajoin('РегистрПродажи', 'Добавляем КурсUSD');

Call bjoin('РегистрПродажи');
LEFT JOIN (РегистрПродажи)
LOAD
    *
    ,Round((RangeMax([ЦенаПартии], 0) * RangeMax([Продано единиц], 0) * Курс / [КурсUSDпр]), 0.01) as СтоимостьПартии
    ,Round((RangeMax([ЦенаПартии], 0) * Курс / [КурсUSDпр]), 0.01) as ПрайсоваяЦена
    ,Round([Стоимость] / [Продано единиц], 0.01) as Цена
    ,Round((RangeMin(RangeMax([Продано единиц], 0), 1) * [Стоимость]) - Round((RangeMax([ЦенаПартии], 0) * RangeMax([Продано единиц], 0) * Курс / [КурсUSDпр]), 0.01), 0.01) as СкидкаОтПрайса
RESIDENT РегистрПродажи;
Call ajoin('РегистрПродажи', 'Рассчитываем СтоимостьПартии, Цена, СкидкаОтПрайса');

DROP FIELDS Курс FROM РегистрПродажи;

[Курсы]:
LOAD
    %ДатаКурса as %Дата
    ,КурсUSD2 as КурсUSD
RESIDENT КурсUSD;
DROP Table КурсUSD;

[Валюты]:
LOAD
    AutoNumber(_IDRRef, 'Валюта') as %ВалютаЗакупкиИмпорт
    ,_Description as Валюта
;SQL SELECT
    _IDRRef
    ,_Description
FROM $(vBaseName).dbo._Reference22; //Справочник_Валюты

LET vLoad = Time(Time(Now(1)) - vLoad);
TRACE
Время загрузки: $(vLoad)
;